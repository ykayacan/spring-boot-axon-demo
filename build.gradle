plugins {
    id 'org.springframework.boot' version '2.3.0.RC1' apply false
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'

    group = 'io.github.ykayacan'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = JavaVersion.VERSION_14

    repositories {
        mavenCentral()
        maven { url 'https://repo.spring.io/milestone' }
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs += '--enable-preview'
    }

    tasks.withType(Test) {
        jvmArgs += "--enable-preview"
    }

    dependencies {
        implementation platform('org.springframework.boot:spring-boot-dependencies:2.3.0.RC1')
    }

    test {
        useJUnitPlatform()
        testLogging {
            events 'PASSED', 'FAILED', 'SKIPPED'
        }
        afterSuite { desc, result ->
            if (!desc.parent) {
                println "\nTest result: ${result.resultType}"
                println "Test summary: ${result.testCount} tests, " +
                        "${result.successfulTestCount} succeeded, " +
                        "${result.failedTestCount} failed, " +
                        "${result.skippedTestCount} skipped"
            }
        }
    }
}
